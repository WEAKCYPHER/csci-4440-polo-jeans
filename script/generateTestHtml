#!/usr/local/bin/coffee

fs = require 'fs'

String.prototype.startsWith = (s) ->
	@substring(0, s.length) == s

recurseDirectory = (path, callBack) ->
	(fs.readdirSync path).forEach (file) ->
		fullName = "#{path}/#{file}"
		stats = fs.statSync fullName
		if stats.isFile()
			callBack fullName
		else if stats.isDirectory()
			recurseDirectory fullName, callBack

allRecursiveFiles = (path) ->
	files = []
	recurseDirectory path,(fileName) ->
		files.push fileName
	files

scripts = do ->
	meta = (file) ->
		String(file).startsWith 'js/metaProgramming'

	script = (file) ->
		"<script src='#{file}'></script>"

	scriptAll = (files) ->
		(files.map script).join '\n'

	files =
		allRecursiveFiles 'js'

	metas =
		scriptAll(files.filter meta)

	normals =
		scriptAll(files.filter (x) -> not meta x)

	"<!--Meta-programming first-->\n#{metas}\n\n#{normals}"

indent = (str) ->
	str.replace /\n/g, '\n\t'

testHtml = """
<!DOCTYPE html>
<meta charset="UTF-8" />
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">

<head>
	<script src='lib/jasmine-1.3.0/jasmine.js'></script>
	<script src='lib/jasmine-1.3.0/jasmine-html.js'></script>
	<link rel='stylesheet' type='text/css' href='lib/jasmine-1.3.0/jasmine.css'>

	#{indent scripts}
</head>

<body>


</body>
</html>
"""

fs.writeFileSync 'test.html', testHtml
